buildscript {
    dependencies {
        classpath 'pl.allegro.tech.build:axion-release-plugin:1.6.0'
        classpath 'org.jacoco:org.jacoco.core:0.7.4.201502262128'
    }
}

apply plugin: 'pl.allegro.tech.build.axion-release'

group = "com.pij"

scmVersion {
    tag {
        prefix = ''
    }

    versionCreator 'versionWithBranch'
    hooks {
        pre 'fileUpdate', [file: 'README.md', pattern: { previous, context -> />$previous<\// }, replacement: { current, context -> ">$current</" }]
        pre 'fileUpdate', [file: 'README.md', pattern: { previous, context -> /:$previous'/ }, replacement: { current, context -> ":$current\'" }]
        pre 'commit', { v, p -> "Automatic update for release $v" }
        post 'push'
    }
}

allprojects {
    project.version = scmVersion.version
    project.group = rootProject.group
}

ext.deps = [
        // Android
        android            : 'com.google.android:android:2.1.2',
        supportv4          : 'com.android.support:support-v4:23.0.1',
        support_annotations: 'com.android.support:support-annotations:23.0.1',

        // Square
        javapoet           : 'com.squareup:javapoet:1.6.1',

        // Miscellaneous
        commons_lang       : 'org.apache.commons:commons-lang3:3.4',
        commons_collections: 'org.apache.commons:commons-collections4:4.1',

        // Test dependencies
        junit              : 'junit:junit:4.12',
        compiletesting     : 'com.google.testing.compile:compile-testing:0.9',
        autoservice        : 'com.google.auto.service:auto-service:1.0-rc2',
        autocommon         : 'com.google.auto:auto-common:0.6'
]
