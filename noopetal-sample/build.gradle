buildscript {
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.5'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.github.dcendents.android-maven'

calculateVersionCode = { String versionName ->
    def shortVersion = versionName.split("-", 2)[0]
    def strings = shortVersion.split("\\.", 3)
    def numbers = strings.collect {
        try {
            it.toInteger()
        } catch (ignored) {
            0
        }
    }
    while (numbers.size < 3) {
        numbers.add(0)
    }
    def result = 0
    numbers.eachWithIndex { number, i ->
        if (i < 3) {
            result = result * 1000 + number
        }
    }
    result
}


android {
    compileSdkVersion 25

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        applicationId 'com.github.pij.noopetal.example'
        minSdkVersion 9
        //noinspection OldTargetApi
        targetSdkVersion 21
        versionCode calculateVersionCode(project.version)
        versionName project.version
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

//    buildTypes {
//        debug {
//            minifyEnabled true
//            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
//        }
//    }
}

dependencies {
    implementation deps.support_annotations
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation project(':noopetal-annotations')
    annotationProcessor project(':noopetal-compiler')

    testImplementation 'junit:junit:4.12'
    testImplementation 'com.google.truth:truth:0.28'
    testImplementation 'org.robolectric:robolectric:3.6.1'
}
